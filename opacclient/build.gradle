apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'
        classpath 'org.json:json:20090211'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = [":libopac:jacocoTestReport", ":opacapp:jacocoTestReport"]
    executionData.setFrom(files(["libopac/build/jacoco/test.exec", "opacapp/build/jacoco/testGooglePlayServicesDebugUnitTest.exec"]))
    sourceDirectories.setFrom(files(["opacapp/src/main/java", "libopac/src/main/java"]))

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "opacapp/build/intermediates/classes/googlePlayServicesDebug", excludes: fileFilter)
    def libopacDebugTree = fileTree(dir: "libopac/build/classes/java/main")
    classDirectories.setFrom(files([debugTree, libopacDebugTree]))
    jacocoClasspath = configurations.jacocoAnt
    reports {
        html.enabled = true
        html.destination = file("build/reports/jacoco/jacocoRootReport/html")
        xml.enabled = true
        xml.destination = file("build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
    }
}

coveralls {
    sourceDirs = ["opacapp/src/main/java", "libopac/src/main/java"]
    jacocoReportPath = "build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
