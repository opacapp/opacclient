apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "de.geeksfactory.opacclient"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 125
        versionName "4.1.11"
    }

    lintOptions {
        // disable 'AndroidLintRtlHardcoded' // Throws an "Unknown issue id" for me
        disable 'MissingTranslation'
        disable 'NewApi'  // does not respect our annotations at release time :(
        disable 'UnusedAttribute'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "opac client"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles 'proguard-rules.txt'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
        encoding 'UTF-8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    compile(project(':libopac')) {
        transitive = false
    }
    compile('ch.acra:acra:4.5.0') {
        exclude group: 'org.json', module: 'json'
    }
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:palette-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.apmem.tools:layouts:1.8@aar'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.3.6'
    compile files('libs/adapter-1.0.1.jar')
    compile files('libs/endless-1.2.3.jar')
}
