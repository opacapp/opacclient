import de.geeksfactory.opacclient.gradle.JsonFilesTask

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "de.geeksfactory.opacclient"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 184
        versionName "5.2.6"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        // disable 'AndroidLintRtlHardcoded' // Throws an "Unknown issue id" for me
        disable 'MissingTranslation'
        disable 'UnusedAttribute'
        warning 'InvalidPackage'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "opac client"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles 'proguard-rules.txt'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
        encoding 'UTF-8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    useLibrary 'org.apache.http.legacy'

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile(project(':libopac')) {
        transitive = false
    }
    compile('ch.acra:acra:4.9.0') {
        exclude group: 'org.json', module: 'json'
    }
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.android.support:design:27.0.0'
    compile 'com.android.support:support-v4:27.0.0'
    compile 'com.android.support:percent:27.0.0'
    compile 'com.android.support:appcompat-v7:27.0.0'
    compile 'com.android.support:palette-v7:27.0.0'
    compile 'com.android.support:cardview-v7:27.0.0'
    compile 'com.android.support:preference-v7:27.0.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.apmem.tools:layouts:1.8@aar'
    implementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile 'net.opacapp:multiline-collapsingtoolbar:1.6.0'
    compile 'joda-time:joda-time:2.8.2'
    compile 'org.joda:joda-convert:1.8'
    compile 'su.j2e:rv-joiner:1.0.6'
    compile 'joda-time:joda-time:2.8.2'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile "com.squareup.okhttp3:okhttp-urlconnection:3.9.0"
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
    compile 'com.samskivert:jmustache:1.13'
    compile 'com.github.johan12345:material-intro:custom_layout-1.5.5-g47ed124'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'
    compile 'com.evernote:android-job:1.2.0'
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:okhttp:3.9.0'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.1.0'
    testCompile 'commons-io:commons-io:2.5'

    // We don't want to rely on the CommonsWare Maven repo, so we include these libraries as JARs
    compile files('libs/adapter-1.0.1.jar')
    compile files('libs/endless-1.2.3.jar')
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    jacocoClasspath = configurations['androidJacocoAnt']

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")
}

task downloadJson(type: JsonFilesTask)

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn downloadJson
    }
}
